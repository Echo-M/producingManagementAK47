; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "卓目点钞机生产管理系统"
#define MyAppVersion "1.0.2.7"
#define MyAppPublisher "武汉卓目科技有限公司"
#define MyAppURL "http://www.zmvision.com/"
#define MyAppExeName "ccmdbg.exe"
#define MyManufacturerId "{{257A6112-9DAE-4241-922F-E2DBD59E19E3}"
#define MyManufacturerName "卓目"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{B80EDF6E-16E1-4AD3-B863-87B56F35DA37}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\ZMVision\ccmdbg
DisableDirPage=yes
DefaultGroupName={#MyAppName}
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes
;Password=zhuomu642

[Languages]
Name: "chinesesimplified"; MessagesFile: "compiler:Languages\ChineseSimplified.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\Release\ccmdbg.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Release\FtpUploadService.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Release\msvcr120.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Release\model.ini"; DestDir: "{localappdata}\ZMVision\ccmdbg\config"; Flags:ignoreversion

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\FtpUploadService.exe"; Parameters: "install"; Flags: waituntilterminated
Filename: "{app}\{#MyAppExeName}"; Flags: nowait postinstall skipifsilent; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"

[UninstallRun]
Filename: "{app}\FtpUploadService.exe"; Parameters: "uninstall"; Flags: waituntilterminated

[Registry]
Root: "HKCU"; Subkey: "SOFTWARE\ZMVision\{#MyAppName}"; ValueType: string; ValueName: "ManufactureId"; ValueData: "{#MyManufacturerId}"; Flags: uninsdeletevalue
Root: "HKCU"; Subkey: "SOFTWARE\ZMVision\{#MyAppName}"; ValueType: string; ValueName: "ManufactureName"; ValueData: "{#MyManufacturerName}"; Flags: uninsdeletevalue
Root: "HKCU"; Subkey: "SOFTWARE\ZMVision\{#MyAppName}"; ValueType: string; ValueName: "InstallDirectory"; ValueData: "{app}"; Flags: uninsdeletevalue
Root: "HKCU"; Subkey: "SOFTWARE\ZMVision\{#MyAppName}"; ValueType: string; ValueName: "Version"; ValueData: "{#MyAppVersion}"; Flags: uninsdeletevalue

[INI]
Filename: "{app}\FTPCONFIG.ini"; Section: "Config"; Key: "ServerIP"; String: "factory.zmvision.cn"
Filename: "{app}\FTPCONFIG.ini"; Section: "Config"; Key: "ServerPort"; String: "21"
Filename: "{app}\FTPCONFIG.ini"; Section: "Config"; Key: "User"; String: ""
Filename: "{app}\FTPCONFIG.ini"; Section: "Config"; Key: "Password"; String: ""
Filename: "{app}\FTPCONFIG.ini"; Section: "Config"; Key: "RemotePath"; String: "/"
Filename: "{app}\FTPCONFIG.ini"; Section: "Config"; Key: "RemoteTerminalPath"; String: "/terminal/"

Filename: "{app}\FTPCONFIG.ini"; Section: "Upload"; Key: "ServerIP"; String: "factory.zmvision.cn"
Filename: "{app}\FTPCONFIG.ini"; Section: "Upload"; Key: "ServerPort"; String: "21"
Filename: "{app}\FTPCONFIG.ini"; Section: "Upload"; Key: "User"; String: ""
Filename: "{app}\FTPCONFIG.ini"; Section: "Upload"; Key: "Password"; String: ""
Filename: "{app}\FTPCONFIG.ini"; Section: "Upload"; Key: "LocalPath"; String: "{localappdata}\ZMVision\ccmdbg\log\"
Filename: "{app}\FTPCONFIG.ini"; Section: "Upload"; Key: "Filter"; String: "dat"
Filename: "{app}\FTPCONFIG.ini"; Section: "Upload"; Key: "RemotePath"; String: "/file/"


[UninstallDelete]
Type: files; Name: "{app}\FTPCONFIG.ini"

[Dirs]
Name: "{localappdata}\ZMVision\ccmdbg\config"; Flags: uninsalwaysuninstall
Name: "{localappdata}\ZMVision\ccmdbg\log"; Flags: uninsalwaysuninstall

[Code]
function InitializeSetup(): boolean;
var
  ResultStr: String;
  ResultCode: Integer;
begin
  if RegQueryStringValue(HKCU, 'SOFTWARE\ZMVision\{#MyAppName}', 'InstallDirectory', ResultStr) then
    begin
      ResultStr := ResultStr + '\FtpUploadService.exe';
      Exec(ResultStr, 'uninstall', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
    end;
    result := true;
end;